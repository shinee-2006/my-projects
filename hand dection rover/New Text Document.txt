#include <WiFi.h>
#include <WebServer.h>

const char* ssid = "ESP32_GESTURE";
const char* password = "12345678";

#define IN1 25
#define IN2 26
#define ENA 33

#define IN3 27
#define IN4 14
#define ENB 32

WebServer server(80);

int speedVal = 200;

void stopRobot()
{
  digitalWrite(IN1, LOW); digitalWrite(IN2, LOW);
  digitalWrite(IN3, LOW); digitalWrite(IN4, LOW);
}

void forward()
{
  digitalWrite(IN1, HIGH); digitalWrite(IN2, LOW);
  digitalWrite(IN3, HIGH); digitalWrite(IN4, LOW);
}

void backward()
{
  digitalWrite(IN1, LOW); digitalWrite(IN2, HIGH);
  digitalWrite(IN3, LOW); digitalWrite(IN4, HIGH);
}

void left()
{
  digitalWrite(IN1, LOW); digitalWrite(IN2, HIGH);
  digitalWrite(IN3, HIGH); digitalWrite(IN4, LOW);
}

void right()
{
  digitalWrite(IN1, HIGH); digitalWrite(IN2, LOW);
  digitalWrite(IN3, LOW); digitalWrite(IN4, HIGH);
}

void handleRoot()
{
  server.send(200, "text/html",
    "<h2>Gesture Controlled Rover</h2>"
    "<p>Commands:</p>"
    "<ul>"
    "<li>/one - Forward</li>"
    "<li>/two - Backward</li>"
    "<li>/three - Left</li>"
    "<li>/four - Right</li>"
    "<li>/stop - Stop</li>"
    "</ul>");
}

void setup()
{
  Serial.begin(115200);

  pinMode(IN1, OUTPUT); pinMode(IN2, OUTPUT);
  pinMode(IN3, OUTPUT); pinMode(IN4, OUTPUT);
  pinMode(ENA, OUTPUT); pinMode(ENB, OUTPUT);

  analogWrite(ENA, speedVal);
  analogWrite(ENB, speedVal);

  stopRobot();

  WiFi.softAP(ssid, password);
  Serial.print("ESP32 IP Address: ");
  Serial.println(WiFi.softAPIP());

  server.on("/", handleRoot);

  server.on("/one", [](){
    forward();
    server.send(200, "text/plain", "Forward");
  });

  server.on("/two", [](){
    backward();
    server.send(200, "text/plain", "Backward");
  });

  server.on("/three", [](){
    left();
    server.send(200, "text/plain", "Left");
  });

  server.on("/four", [](){
    right();
    server.send(200, "text/plain", "Right");
  });

  server.on("/stop", [](){
    stopRobot();
    server.send(200, "text/plain", "Stop");
  });

  server.begin();
}

void loop()
{
  server.handleClient();
}
