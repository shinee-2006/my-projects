#define BLYNK_PRINT Serial
#include <ESP8266WiFi.h>
#include <BlynkSimpleEsp8266.h>
#include <DHT.h>
char auth[] = "YOUR_BLYNK_AUTH_TOKEN";
char ssid[] = "YOUR_WIFI_NAME";
char pass[] = "YOUR_WIFI_PASSWORD";
#define DHTPIN D4
#define DHTTYPE DHT11
#define RELAY_PIN D1
#define SOIL_PIN A0
DHT dht(DHTPIN, DHTTYPE);
BlynkTimer timer;
int soilValue;
float temperature;
float humidity;
void readSensors()
{
  humidity = dht.readHumidity();
  temperature = dht.readTemperature();
  soilValue = analogRead(SOIL_PIN);

  Blynk.virtualWrite(V0, temperature);
  Blynk.virtualWrite(V1, humidity);
  Blynk.virtualWrite(V2, soilValue);

  Serial.print("Temp: ");
  Serial.print(temperature);
  Serial.print(" C  Humidity: ");
  Serial.print(humidity);
  Serial.print(" %  Soil: ");
  Serial.println(soilValue);
  if (soilValue > 700)
  {
    digitalWrite(RELAY_PIN, LOW); 
    Blynk.virtualWrite(V3, 1);
  }
  else
  {
    digitalWrite(RELAY_PIN, HIGH);
    Blynk.virtualWrite(V3, 0);
  }
}

void setup()
{
  Serial.begin(9600);
  pinMode(RELAY_PIN, OUTPUT);
  digitalWrite(RELAY_PIN, HIGH);
  dht.begin();
  Blynk.begin(auth, ssid, pass);
  timer.setInterval(2000L, readSensors);
}
void loop()
{
  Blynk.run();
  timer.run();
}
